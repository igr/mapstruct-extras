plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.30.0'
}

repositories {
    mavenCentral()
}

group = 'com.oblac.mapstruct'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    implementation 'org.mapstruct:mapstruct-processor:1.4.2.Final'
}

tasks.named('test') {
    useJUnitPlatform()
}

ext.admin = hasProperty('sonatypeUsername')

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'with-not-fluent'
            from components.java

            pom {
                name = 'MapStruct With Not Fluent'
                description = '@With is not part of a fluent interface'
                url = 'https://github.com/igr/mapstruct-extras'
                organization {
                    name = 'Oblac'
                    url = 'https://oblac.com'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/igr/mapstruct-extras/issues'
                }
                licenses {
                    license {
                        name = 'BSD-2-Clause'
                        url = 'https://github.com/igr/mapstruct-extras/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/igr/mapstruct-extras'
                    connection = 'scm:git:git://github.com/igr/mapstruct-extras.git'
                    developerConnection = 'scm:git:ssh://git@github.com/igr/mapstruct-extras.git'
                }
                developers {
                    developer {
                        id = 'igor'
                        name = 'Igor SpasiÄ‡'
                        email = 'igor@jodd.org'
                        timezone = '+1'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            if (admin) {
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}

task install() {
    group 'Project'
    dependsOn publishToMavenLocal
}

signing {
    required { admin }
    sign publishing.publications.maven
}

task release() {
    group 'Project'
    description 'Builds everything for the release.'

    dependsOn build
    dependsOn javadoc
}